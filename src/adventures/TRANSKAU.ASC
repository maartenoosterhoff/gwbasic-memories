10 REM *********************************
20 REM * HET RAADSEL VAN TRANSKAUKAZIE *
30 REM *********************************
40 CLS:COLOR 14,0:GOSUB 8000:REM INITIATIE
50 PRINT "Wilt u de landkaart op papier (j/n) ?":PRINT
60 I$=INKEY$:IF I$="" THEN GOTO 60
70 IF I$="j" OR I$="J" THEN GOSUB 9100:GOTO 100
80 IF I$="n" OR I$="N" THEN GOTO 100
90 GOTO 60
100 CLS:GOSUB 1500:GOSUB 5000:REM KIJK
110 GOSUB 1000:REM COMMANDO VERTALER
120 GOSUB 3000:REM ACTIES
130 GOSUB 5000:REM TEGEN ACTIES
140 IF FLOW>0 THEN GOTO 110
150 PRINT "U bent helaas aan uw wonden overleden .":PRINT
160 PRINT:PRINT "Druk op een toets voor Menu":PRINT
170 COLOR 8,0:PRINT INPUT$(1):COLOR 14,0
180 RUN "D:\BASIC\BP.BAS"
1000 REM *********************
1010 REM * COMMANDO VERTALER *
1020 REM *********************
1030 PRINT: INPUT "Wat doet u ";C$
1040 IF C$="" THEN GOTO 1030
1045 IF C$="*" THEN RETURN :REM VORIGE OPDRACHT
1050 PRINT:PO=LEN(C$)+1
1060 FOR I=1 TO LEN(C$)
1070 IF ASC(MID$(C$,I))=32 THEN PO=I
1080 NEXT I
1090 C1=0: LET W$=MID$ (C$,1,PO-1)
1100 IF W$="noord"      THEN C1=1:GOTO 1190
1110 IF W$="zuid"       THEN C1=2:GOTO 1190
1120 IF W$="oost"       THEN C1=3:GOTO 1190
1130 IF W$="west"       THEN C1=4:GOTO 1190
1140 IF W$="boven"      THEN C1=5:GOTO 1190
1150 IF W$="beneden"    THEN C1=6:GOTO 1190
1160 IF W$="pak"        THEN C1=7:GOTO 1190
1170 IF W$="leg"        THEN C1=8:GOTO 1190
1180 IF W$="inventaris" THEN C1=9:GOTO 1190
1181 IF W$="dood"       THEN C1=10:GOTO 1190
1182 IF W$="vecht"      THEN C1=11:GOTO 1190
1183 IF W$="wacht"      THEN C1=12:GOTO 1190
1184 IF W$="gebruik"    THEN C1=13:GOTO 1190
1185 IF W$="kijk"      THEN C1=14:GOTO 1190
1190 IF C1=0 THEN PRINT "Ik begrijp niet wat u bedoelt":GOTO 1030
1200 IF PO=LEN(C$)+1 THEN C2=0:RETURN
1210 C2=0: LET W$=MID$(C$,PO+1,LEN(C$))
1220 IF W$="verband"   THEN C2=1:GOTO 1310
1230 IF W$="water"     THEN C2=2:GOTO 1310
1240 IF W$="zwaard"    THEN C2=3:GOTO 1310
1245 IF W$="zwachtel"  THEN C2=4:GOTO 1310
1250 IF W$="touw"      THEN C2=5:GOTO 1310
1260 IF W$="schild"    THEN C2=6:GOTO 1310
1270 IF W$="ring"      THEN C2=7:GOTO 1310
1280 IF W$="sleutel"   THEN C2=8:GOTO 1310
1290 IF W$="drank"     THEN C2=9:GOTO 1310
1300 IF W$="halssnoer" THEN C2=10:GOTO 1310
1305 IF W$="monster"   THEN C2=11:GOTO 1310
1306 IF W$="wolf"      THEN C2=12:GOTO 1310
1310 IF C2=0 THEN RETURN
1320 RETURN
1500 REM ********
1510 REM * KIJK *
1520 REM ********
1530 PRINT
1540 IF KA<=7 THEN ON KA GOSUB 2000,2020,2040,2060,2090,2100,2110:GOTO 1630
1550 IF KA<=14 THEN ON KA-7 GOSUB 2120,2130,2140,2150,2160,2190,2200:GOTO 1630
1560 IF KA<=21 THEN ON KA-14 GOSUB 2210,2240,2250,2270,2290,2300,2310:GOTO 1630
1570 IF KA<=28 THEN ON KA-21 GOSUB 2330,2350,2420,2450,2470,2480,2500:GOTO 1630
1580 IF KA<=35 THEN ON KA-28 GOSUB 2510,2530,2550,2570,2590,2620,2630:GOTO 1630
1590 IF KA<=42 THEN ON KA-35 GOSUB 2640,2660,2680,2700,2720,2740,2750:GOTO 1630
1600 IF KA<=49 THEN ON KA-42 GOSUB 2770,2780,2790,2800,2810,2820,2840:GOTO 1630
1610 IF KA<=56 THEN ON KA-49 GOSUB 2850,2860,2870,2880,2890,2900,2920:GOTO 1630
1620 IF KA=57 THEN ON KA-56 GOSUB 2940:GOTO 1630
1630 PRINT:PRINT "Uitgangen :";
1640 FOR A=1 TO 6
1650 IF K% (KA,A)<>0 THEN PRINT TAB(13);RI$(A)
1660 NEXT A:PRINT
1670 D=0:PRINT :PRINT "Hier ligt ";
1680 FOR I=1 TO NO
1690 IF KA=OB%(I) THEN PRINT "een ";T$(I);:D=1
1700 NEXT I
1710 IF D=0 THEN PRINT "geen voorwerp ."
1720 PRINT
1730 RETURN
2000 PRINT "U staat aan de Noord-West punt van ":PRINT  ' kamer 1
2010 PRINT "een groot bos .":PRINT:RETURN
2020 PRINT "U staat in een groot donker bos ":PRINT ' kamer 2
2030 PRINT "aan de Noord zijde  .":PRINT:RETURN
2040 PRINT "U staat aan de rand van een":PRINT ' kamer 3
2050 PRINT "woest bos met vele dieren .":PRINT:RETURN
2060 PRINT "U staat aan de Noord-Oostelijke":PRINT ' kamer 4
2070 PRINT "zijde van een bos . In het Oosten":PRINT
2080 PRINT "ziet u een oude vervallen hut .":PRINT:RETURN
2090 PRINT "U bent in de oude vervallen hut van de smid .":PRINT:RETURN ' kamer 5
2100 PRINT "U staat aan de West rand van het bos .":PRINT:RETURN ' kamer 6
2110 PRINT "U staat in een open plek in het bos .":PRINT:RETURN ' kamer 7
2120 PRINT "U staat midden in het grote bos .":PRINT:RETURN ' kamer 8
2130 PRINT "U staat aan een beek met schoon helder water .":PRINT:RETURN ' kamer 9
2140 PRINT "U bent in de werkplaats van de smid .":PRINT:RETURN ' kamer 10
2150 PRINT "U staat aan de Zuid-West rand van het bos .":PRINT:RETURN ' kamer 11
2160 PRINT "U staat in het grote bos aan de Zuid rand .":PRINT
2170 PRINT "In de verte ziet u een groot kasteel met klimop-":PRINT
2180 PRINT "planten eraan gegroeid .":PRINT:RETURN ' kamer 12
2190 PRINT "U staat aan de Zuid rand van het grote bos .":PRINT:RETURN ' kamer 13
2200 PRINT "U staat aan de rand van het grote bos .":PRINT:RETURN ' kamer 14
2210 PRINT "U staat op een groot heideveld . In het ":PRINT
2220 PRINT "zuiden ziet u een groot kasteel dat met ":PRINT
2230 PRINT "klimopplanten begroeid is .":PRINT:RETURN ' kamer 15
2240 PRINT "U staat in Noord-West uitkijktoren van het kasteel .":PRINT:RETURN ' kamer 16
2250 PRINT "U staat op de kantelen van de kasteelmuur ":PRINT
2260 PRINT "aan de Noord kant .":PRINT:RETURN ' kamer 17
2270 PRINT "U staat op de kantelen in het midden ":PRINT
2280 PRINT "op de kasteelmuur .":PRINT:RETURN ' kamer 18
2290 PRINT "U staat links van de uitkijktoren op de kantelen .":PRINT:RETURN ' kamer 19
2300 PRINT "U bent in de Noord-Oost uitkijktoren van het kasteel . ":PRINT:RETURN ' kamer 20
2310 PRINT "U staat op de kantelen aan de West kant van ":PRINT
2320 PRINT "het kasteel . ":PRINT:RETURN ' kamer 21
2330 PRINT "U staat in de stal van het kasteel .":PRINT
2340 PRINT "Er hangt een vieze poeplucht .":PRINT:RETURN ' kamer 22
2350 PRINT "U komt in een donkere kamer . Als u ":PRINT
2360 PRINT "in het midden van de kamer bent , valt ":PRINT
2370 PRINT "er een kooi van het plafond waaruit u":PRINT
2380 PRINT "nooit meer ontsnapt . U ligt er tot u":PRINT
2390 PRINT "doodgaat van honger . Einde .":PRINT
2400 PRINT:PRINT "Druk op een toets voor menu":COLOR 8,0:PRINT INPUT$(1)
2410 COLOR 14,0:RUN "D:\BASIC\BP.BAS" 'kamer 23
2420 PRINT "U staat in de opslagplaats van het kasteel .":PRINT
2430 PRINT "Helaas is al het voedsel of bedorven ":PRINT
2440 PRINT "of opgegeten door de ratten .":PRINT:RETURN ' kamer 24
2450 PRINT "U staat op de kantelen van de Noord-Oost kant":PRINT
2460 PRINT "van het kasteel .":PRINT:RETURN ' kamer 25
2470 PRINT "U staat op de kantelen van het kasteel .":PRINT:RETURN ' kamer 26
2480 PRINT "U staat in de gevangenis van het kasteel .":PRINT
2490 PRINT "In vele hokken liggen menselijke beenderen .":PRINT:RETURN ' kamer 27
2500 PRINT "U staat op de binnenplaats van het kasteel .":PRINT:RETURN ' kamer 28
2510 PRINT "U bent in een kamer van vuur . U komt er ":PRINT
2520 PRINT "gelukkig heelhuids doorheen .":PRINT:RETURN ' kamer 29
2530 PRINT "U staat op de kantelen van het ":PRINT
2540 PRINT "grote kasteel .":PRINT:RETURN ' kamer 30
2550 PRINT "U staat op de kantelen van het ":PRINT
2560 PRINT "kasteel , vlakbij een uitkijktoren .":PRINT:RETURN ' kamer 31
2570 PRINT "U bent in een drankopslagplaats .":PRINT
2580 PRINT "De meeste drank is nog goed .":PRINT:RETURN ' kamer 32
2590 PRINT "Nadat u de kamer hebt betreden , valt u":PRINT
2600 PRINT "in slaap . U wordt helaas niet meer ":PRINT
2610 PRINT "wakker .":PRINT INPUT$(1):RUN"D:\BASIC\BP.BAS"  ' kamer 33
2620 PRINT "U bent in het woongedeelte van het kasteel .":PRINT:RETURN ' kamer 34
2630 PRINT "U staat op de kantelen van het kasteel .":PRINT:RETURN ' kamer 35
2640 PRINT "U bent in de Zuid-West uitkijktoren ":PRINT
2650 PRINT "van het kasteel .":PRINT:RETURN ' kamer 36
2660 PRINT "U staat op de Zuidelijke kantelen ":PRINT
2670 PRINT "van het kasteel .":PRINT:RETURN ' kamer 37
2680 PRINT "U staat precies tussen twee uitkijk-":PRINT
2690 PRINT "torens in op de kantelen .":PRINT:RETURN ' kamer 38
2700 PRINT "U staat links van de Zuid-Oost uitkijktoren .":PRINT
2710 PRINT "op de kantelen van het kasteel .":PRINT:RETURN ' kamer 39
2720 PRINT "U bent in de Zuid-Oost uitkijktoren van ":PRINT
2730 PRINT "het kasteel .":PRINT:RETURN ' kamer 40
2740 PRINT "U staat in een vieze rioolingang ":PRINT:RETURN ' kamer 41
2750 PRINT "U bent in een riool vol met grote ":PRINT
2760 PRINT "ratten .":PRINT:RETURN ' kamer 42
2770 PRINT "U staat in een stinkend riool met veel schimmel ":PRINT:RETURN ' kamer 43
2780 PRINT "U bent in een klein hokje van een riool .":PRINT:RETURN ' kamer 44
2790 PRINT "U staat in een grote rioolgang .":PRINT:RETURN ' kamer 45
2800 PRINT "U staat in een vies riool ":PRINT:RETURN ' kamer 46
2810 PRINT "U staat in een riool vol met ratten .":PRINT:RETURN ' kamer 47
2820 PRINT "U staat in een riool . U ziet ":PRINT
2830 PRINT "een dode rat voorbij drijven .":PRINT:RETURN ' kamer 48
2840 PRINT "U staat in een stinkend riool ":PRINT:RETURN ' kamer 49
2850 PRINT "U staat in een vies rioolgangetje .":PRINT:RETURN ' kamer 50
2860 PRINT "U staat in een vies rioolgangetje .":PRINT:RETURN ' kamer 51
2870 PRINT "U staat in een vies rioolgangetje .":PRINT:RETURN ' kamer 52
2880 PRINT "U staat in een riool vol grote ratten .":PRINT:RETURN ' kamer 53
2890 PRINT "U staat in een riool vol grote ratten .":PRINT:RETURN ' kamer 54
2900 PRINT "U staat in een vies riool waar u ":PRINT
2910 PRINT "allerlei gore ziektes kunt oplopen .":PRINT:RETURN ' kamer 55
2920 PRINT "U staat in een betrekkelijk schoon ":PRINT
2930 PRINT "kamertje .":PRINT:RETURN ' kamer 56
2940 PRINT "U staat in de schatkamer , u bent de eerste":PRINT
2950 PRINT "die dit ooit heeft gehaald GEFELICITEERD !!":PRINT
2960 PRINT:PRINT "Druk op een toets voor Menu ":COLOR 8,0:PRINT INPUT$(1):COLOR 14,0
2970 RUN "D:\BASIC\BP.BAS"
3000 REM **********
3010 REM * ACTIES *
3020 REM **********
3030 IF C1<=6 THEN GOSUB 3100:GOTO 3050
3040 IF C1<=13 THEN ON C1-6 GOSUB 3200,3300,3400,3500,3500,3800,3900:GOTO 3050
3045 IF C1=14 THEN GOSUB 1500
3050 RETURN
3100 REM * POSITIE VERANDERING *
3110 IF K% (KA,C1)=0 THEN PRINT "Daar kunt u niet heen .":PRINT:RETURN
3120 KA=K% (KA,C1)
3130 GOSUB 1500: REM KIJK
3140 RETURN
3200 REM * PAK *
3210 IF C2=0 THEN PRINT "Dat kunt u niet pakken .":PRINT:RETURN
3220 IF OB%(C2)<>KA THEN PRINT "Dat ligt hier niet .":PRINT:RETURN
3230 IF RL<=GEW%(C2)-1 THEN PRINT "Zoveel kunt u niet meer dragen .":PRINT:RETURN
3240 OB% (C2)=0: REM PLAATS 0 IS DE SPELER
3250 PRINT:PRINT "U pakt het voorwerp .":PRINT
3260 RL=RL-GEW% (C2)
3270 RETURN
3300 REM * LEG *
3310 IF C2=0 THEN PRINT "Dat gaat niet .":PRINT:RETURN
3320 IF OB%(C2)<>0 THEN PRINT "Dat heeft u niet .":PRINT:RETURN
3330 RL=RL+GEW%(C2)
3340 OB%(C2)=KA
3350 PRINT "Nu ligt het voorwerp op de grond .":PRINT
3360 RETURN
3400 REM * INVENTARIS *
3410 D=0:PRINT:PRINT "U heeft ";
3420 FOR I=1 TO NO
3430 IF OB%(I)=0 THEN PRINT "een ";T$(I);" ";:D=1
3440 NEXT I
3450 IF D=0 THEN PRINT "niets ."
3455 PRINT
3465 PRINT:PRINT "U kunt nog";FLOW;:PRINT "wonden overleven .":PRINT
3475 PRINT:PRINT "U kunt nog een gewicht van";RL;
3485 PRINT "eenheden dragen .":PRINT
3490 RETURN
3500 REM * VECHT *
3510 IF C2<=10 THEN PRINT "Daar kunt u niet tegen vechten .":PRINT:RETURN
3520 IF KA=13 OR KA=28 THEN GOTO 3550
3530 PRINT "Uw tegenstander is niet aanwezig .":PRINT:RETURN
3550 HC=1:IF OB%(3)=0 THEN HC=2:REM ZWAARD
3551 IF KA=13 AND WOLF<=0 THEN PRINT "Uw tegenstander is niet aanwezig .":PRINT:RETURN
3552 IF KA=28 AND MS<=0 THEN PRINT "Uw tegenstander is niet aanwezig .":PRINT:RETURN
3560 PRINT:PRINT "U valt ";:IF HC=2 THEN PRINT "met uw zwaard ";
3570 PRINT "uw tegenstander aan .":PRINT
3580 IF KA=28 THEN GOTO 3660
3590 IF RND>.1 THEN GOTO 3610
3600 PRINT "In uw geestdrift mist u uw tegenstander volkomen .":PRINT:RETURN
3610 PRINT "U brengt uw tegenstander een daverende slag toe .":PRINT
3620 WOLF=WOLF-HC
3630 IF WOLF=1 THEN PRINT "Uw tegenstander is zwaar gewond .":PRINT
3640 IF WOLF<=0 THEN PRINT "Uw tegenstander is overleden .":PRINT
3650 RETURN
3660 IF RND>.1 THEN GOTO 3680
3670 PRINT "In uw geestdrift mist u uw tegenstander volkomen .":PRINT:RETURN
3680 PRINT "U brengt uw tegenstander een daverende slag toe .":PRINT
3685 IF OB%(6)=0 THEN PRINT "Uw schild gaf u extra kracht .":PRINT:HC=HC+1
3690 MS=MS-HC
3700 IF MS=1 THEN PRINT "Uw tegenstander is zwaar gewond .":PRINT
3710 IF MS<=0 THEN PRINT "Uw tegenstander is overleden .":PRINT
3720 RETURN
3800 REM * WACHT *
3820 PRINT "U wacht een tijdje .":PRINT
3830 RETURN
3900 REM * GEBRUIK *
3910 IF C2=6 OR C2=11 OR C2=12 THEN PRINT "Dat is niet te gebruiken .":PRINT:RETURN
3920 IF OB%(C2)<>0 THEN PRINT "Dat heeft u niet !":PRINT:RETURN
3930 ON C2 GOSUB 4000,4200,4300,4850,4400,10,4500,4600,4700,4800:GOTO 3940
3940 RETURN
4000 REM * VERBAND *
4010 OB%(C2)=100:RL=RL+GEW%(C2) :REM VERBRUIKT
4020 IF KA<>SU%(4,1) OR SU%(4,2)<=0 THEN GOTO 4040
4025 IF SU%(4,1)<=0 THEN GOTO 4040
4030 PRINT "U gebruikt het verband om de smid te verbinden .":PRINT:SU%(4,2)=2:RETURN
4040 IF KA<>13 THEN GOTO 4070
4050 IF WOLF>0 THEN GOTO 4070
4060 IF REIZ>1 THEN GOTO 4070
4065 PRINT "U gebruikt het verband om de handelsreiziger te verbinden .":PRINT:REIZ=3:RETURN
4070 IF FLOW=7 THEN PRINT "U eet het verband op .":PRINT:RETURN
4080 PRINT "U gebruikt het verband om uw wonden te verbinden .":PRINT
4090 PRINT "U knapt er aardig van op .":PRINT
4100 FLOW=7
4110 RETURN
4200 REM * WATER *
4210 OB%(C2)=9:RL=RL+GEW%(C2) :REM VERBRUIKT
4220 IF KA<>27 THEN GOTO 4270
4230 IF GEV>1 THEN GOTO 4270
4240 PRINT "U geeft het water aan de gevange in het kasteel .":GEV=3:PRINT
4250 RETURN
4270 PRINT "U drinkt het water op .":PRINT:IF FLOW=7 THEN RETURN
4280 FLOW=FLOW+1
4290 RETURN
4300 REM * ZWAARD *
4310 PRINT "U probeert met uw zwaard uw nagels bij":PRINT
4320 PRINT "te werken , hetgeen nogal onhandig gaat .":PRINT:RETURN
4400 REM * TOUW *
4410 IF KA=12 OR KA=15 THEN GOTO 4430
4420 PRINT "Na 1 minuut touwtje springen gaat u weer verder .":PRINT:RETURN
4430 IF KA=15 THEN GOTO 4470
4440 PRINT "U gooit het touw omhoog , en het blijft":PRINT
4450 PRINT "in een tak -vlakbij de muur- vastzitten .":PRINT
4460 K% (12,2)=17:K% (17,1)=12:OB% (C2)=12:RL=RL+GEW%(C2):RETURN
4470 PRINT "U gooit het touw omhoog , en het blijft":PRINT
4480 PRINT "in een tak -vlakbij de muur- vastzitten .":PRINT
4490 K% (15,2)=20:K% (20,1)=15:OB% (C2)=15:RL=RL+GEW%(C2):RETURN
4500 REM * RING *
4510 PRINT "U schuift de ring om uw vinger .":PRINT
4520 PRINT "Hij geeft u wel een sierlijk aanblik ":PRINT
4530 PRINT "U stopt hem weer weg om te zorgen dat u":PRINT
4540 PRINT "hem niet verliest .":PRINT:RETURN
4600 REM * SLEUTEL *
4610 IF KA=56 THEN GOTO 4640
4620 PRINT "Dat is niet nu te gebruiken !":PRINT
4630 RETURN
4640 PRINT "U steekt de sleutel in het slot en draait":PRINT
4650 PRINT "hem een 360๘ . De deur gaat open .":PRINT
4660 K% (56,2)=57:PRINT "De sleutel blijft in het slot vastzitten .":OB%(C2)=100:RL=RL+GEW%(C2):RETURN
4700 REM * DRANK *
4710 OB%(C2)=100:RL=RL+GEW%(C2): REM VERBRUIKT
4715 IF FLOW=7 THEN GOTO 4740
4720 PRINT "U gebruikt de drank om uw wonden te":PRINT
4730 PRINT "ontsmetten . U knapt er aardig van op .":FLOW=7:RETURN
4740 PRINT "U drinkt de drank op . U wordt wakker met":PRINT
4750 PRINT "zware hoofdpijn .":PRINT:FLOW=FLOW-1:RETURN
4800 REM * HALSSNOER *
4810 PRINT "U hangt het halssnoer om uw hals .":PRINT
4820 PRINT "Hij geeft u wel een sierlijk aanblik ":PRINT
4830 PRINT "U stopt hem weer weg om te zorgen dat u":PRINT
4840 PRINT "hem niet verliest .":PRINT:RETURN
4850 REM * ZWACHTEL *
4860 OB%(C2)=100:RL=RL+GEW%(C2) :REM VERBRUIKT
4870 IF KA<>SU%(4,1) OR SU%(4,2)<=0 THEN GOTO 4890
4875 IF SU%(4,1)<=0 THEN GOTO 4890
4885 PRINT "U gebruikt de zwachtel om de smid te verbinden .":PRINT:SU%(4,2)=2:RETURN
4890 IF KA<>13 THEN GOTO 4920
4900 IF WOLF>0 THEN GOTO 4920
4910 IF REIZ=1 THEN GOTO 4915 ELSE GOTO 4920
4915 PRINT "U gebruikt de zwachtel om de handelsreiziger te verbinden .":PRINT:REIZ=3:RETURN
4920 IF FLOW=7 THEN PRINT "U eet de zwachtel op .":PRINT:RETURN
4930 PRINT "U gebruikt de zwachtel om uw wonden te verbinden .":PRINT
4940 PRINT "U knapt er aardig van op .":PRINT
4950 FLOW=7
4960 RETURN
5000 REM ****************
5010 REM * TEGEN ACTIES *
5020 REM ****************
5030 PRINT
5040 IF KA=5 OR KA=10 THEN GOSUB 5800:GOTO 5090
5050 IF KA=27 THEN GOSUB 5200:GOTO 5090
5060 IF KA=13 THEN GOSUB 5400:GOTO 5090
5070 IF KA=28 THEN GOSUB 5600:GOTO 5090
5080 IF KA>40 THEN GOSUB 5100:GOTO 5090
5090 RETURN
5100 REM * RATTEN *
5110 IF RND>.4 THEN RETURN
5120 PRINT "Plotseling hoort u geklots in het":PRINT
5130 PRINT "riool .":PRINT
5140 IF RND<.5 THEN GOTO 5170
5150 PRINT "U wordt aangevallen door een grote rat":PRINT
5160 PRINT "U slaat de rat gelukkig gelijk dood .":PRINT:RETURN
5170 PRINT "U wordt aangevallen door veel ratten .":PRINT
5180 PRINT "Ze slaan toe en verdwijnen dan weer in de blubber .":PRINT:FLOW=FLOW-1
5190 RETURN
5200 REM * GEVANGENE *
5205 IF GEV2=0 THEN GOSUB 5260
5210 IF GEV>0 THEN GOTO 5230
5220 PRINT "Er ligt hier een dode gevangene .":PRINT:RETURN
5230 IF GEV>1 THEN GOTO 5280
5240 PRINT "Er is hier een gevangene ":PRINT
5250 PRINT "Hij vraagt om water . Hij zal u dan belonen .":PRINT:GEV2=GEV2-1:RETURN
5260 PRINT "Met een laatste zucht overlijd de gevangene .":PRINT:GEV=0:GEV2=4
5270 RETURN
5280 IF GEV2=100 THEN RETURN
5290 PRINT "De gevangene legt voor uw voeten als dank een sleutel .":OB%(8)=KA:PRINT
5300 PRINT:PRINT "U maakt het slot open en":PRINT
5310 PRINT "laat de gevangene eruit .":PRINT
5320 PRINT "Dan verdwijnt hij richting de bossen .":PRINT:GEV2=100:RETURN
5400 REM * WOLF/HANDELSREIZIGER *
5405 IF REIZ>0 THEN GOSUB 5500
5410 IF WOLF>0 THEN GOTO 5440
5420 PRINT "Er ligt een dode wolf in de hoek .":PRINT:RETURN
5440 PRINT "Een grote wolf valt u grommend aan .":PRINT
5450 IF RND<.5 THEN GOTO 5470
5460 PRINT "Met een grote sprong kunt u de wolf net ontwijken .":PRINT:RETURN
5470 PRINT "De wolf haalt uit en raakt u hard op uw lichaam .":PRINT
5480 FLOW=FLOW-1
5490 RETURN
5500 IF REIZ2=0 THEN REIZ=0
5505 IF REIZ=3 THEN GOTO 5530
5510 IF REIZ=0 THEN PRINT "Er ligt een dode man ligt in een hoek .":PRINT:RETURN
5520 IF REIZ=1 AND REIZ2>1 THEN PRINT "Een zwaard gewonde man vraagt om hulp .":PRINT:REIZ2=REIZ2-1:RETURN
5525 IF REIZ2=1 THEN PRINT "Met een laatste zucht overlijd de man .":PRINT:REIZ=0:GOTO 5510
5530 IF REIZ2=100 THEN RETURN
5540 PRINT "De handelsreiziger geneest vlug en staat gauw op .":PRINT
5550 PRINT "Hij rommelt in zijn tas en legt met een .":PRINT
5560 PRINT "triomfantelijk gebaar een schild voor uw voeten":PRINT
5570 OB%(6)=KA:PRINT "Dan verdwijnt hij in de bossen .":PRINT:REIZ2=100:RETURN
5600 REM * MONSTER / RING/HALSSNOER *
5610 IF MS<=0 THEN 5670
5620 PRINT "Een gigantisch monster valt u brullend aan .":PRINT
5630 IF RND<.5 THEN GOTO 5650
5640 PRINT "Met een koprol kunt u het monster net ontwijken .":PRINT:RETURN
5650 PRINT "Het monster geeft u een geweldige klap met zijn staart .":PRINT
5660 FLOW=FLOW-1:RETURN
5670 PRINT "Een dood monster ligt in de hoek .":PRINT
5680 K% (28,4)=27:K%(27,3)=28
5690 IF OB%(7)=40 AND OB%(10)=16 THEN K%(28,6)=41:K%(41,5)=28
5700 RETURN
5800 REM * SMID *
5805 IF SU%(4,1)=100 AND SU%(4,3)=0 THEN RETURN
5810 IF SU%(4,2)>0 OR KA<>SU%(4,1) THEN GOTO 5830
5820 PRINT "Er ligt hier een dode smid .":PRINT:RETURN
5830 IF SU%(4,2)=2 THEN SU%(4,3)=30:SU%(4,2)=1
5840 ON INT(SU%(4,3)/10) GOTO 5900,6000,6100,6200,6300,6400
5900 PRINT "Er ligt hier een oude man , die uit":PRINT
5910 PRINT "talrijke wonden bloedt . Met beverige":PRINT
5920 PRINT "stem vraagt hij om hulp .":PRINT
5930 SU%(4,3)=20:RETURN
6000 IF KA=SU%(4,1) THEN PRINT "Met beverige stem vraagt de smid om hulp .":PRINT
6010 SU%(4,3)=SU%(4,3)+1
6020 IF SU%(4,3)=25 THEN SU%(4,3)=60
6030 RETURN
6100 PRINT "De smid geneest wonderbaarlijk snel .":PRINT
6110 PRINT "Weldra springt hij op en roept : ' Volg":PRINT
6120 PRINT "mij . Ik zal u belonen ' . Hij verdwijnt in Zuidelijke richting .":PRINT
6130 SU%(4,3)=40:SU%(4,1)=10:RETURN
6200 SU%(4,3)=SU%(4,3)+1
6210 IF KA<>SU%(4,1) THEN GOTO 6240
6220 PRINT "De smid is druk bezig een schitterend":PRINT
6230 PRINT "zwaard te smeden .":PRINT
6240 IF SU%(4,3)=43 THEN SU%(4,3)=50
6250 RETURN
6300 IF KA<>SU%(4,1) THEN GOTO 6130
6310 PRINT "Met glorieus gebaar legt de smid":PRINT
6320 PRINT "een zwaard aan uw voeten . Dan verdwijnt hij in de bossen .":PRINT
6330 SU%(4,1)=100:SU%(4,3)=0:OB%(3)=10
6340 RETURN
6400 IF KA=SU%(4,1) THEN PRINT "Met een laatste zucht overlijd de smid .":PRINT
6410  SU%(4,2)=0:SU%(4,3)=0:RETURN
8000 REM *************
8010 REM * INITIATIE *
8020 REM *************
8030 KA=1  :REM KAMERNUMMER , START POSITIE
8040 NO=10 :REM AANTAL OBJECTEN
8050 NK=57 :REM AANTAL KAMERS
8060 MS=5  :REM MONSTER LEEFT
8061 SU%(4,1)=5:SU%(4,2)=1:SU%(4,3)=10:REM SMID
8065 WOLF=5:REM WOLF LEEFT
8070 FLOW=7:RL=8:REM SPELER LEEFT
8075 GEV=1:GEV2=4 :REM GEVANGENE LEEFT
8076 REIZ=1:REIZ2=8 :REM HANDELSREIZIGER LEEFT
8080 DIM K% (NK,6), OB%(NO), T$(NO), GEW%(NO)
8100 FOR I=1 TO NK
8110 FOR J=1 TO 6
8120 READ K% (I,J): REM KAMERVERBINDINGEn
8130 NEXT J
8140 NEXT I
8150 FOR K=1 TO NO
8160 READ T$(K),OB%(K),GEW%(K): REM VOORWERP, PLAATS, GEWICHT
8170 NEXT K
8180 FOR A=1 TO 6:READ RI$(A):NEXT A:REM SOORTEN RICHTINGEN
8190 RETURN
8200 REM *********************
8210 REM * KAMERVERBINDINGEN *
8220 REM *********************
8230 REM   N  Z  O  W OP NE :REM KAMERNUMMER
8240 DATA  1, 6, 2, 1, 0, 0 :REM      1
8250 DATA  2, 7, 3, 1, 0, 0 :REM      2
8260 DATA  0, 0, 4, 2, 0, 0 :REM      3
8270 DATA  4, 9, 5, 3, 0, 0 :REM      4
8280 DATA  0,10, 0, 4, 0, 0 :REM      5
8290 DATA  1,11, 0, 6, 0, 0 :REM      6
8300 DATA  2, 0, 8, 0, 0, 0 :REM      7
8310 DATA  0,13, 9, 7, 0, 0 :REM      8
8320 DATA  4, 0, 0, 8, 0, 0 :REM      9
8330 DATA  5, 0, 0, 0, 0, 0 :REM     10
8340 DATA  6, 0,12,11, 0, 0 :REM     11
8350 DATA  0, 0,13,11, 0, 0 :REM     12
8360 DATA  8, 0,14,12, 0, 0 :REM     13
8370 DATA  0, 0,15,13, 0, 0 :REM     14
8380 DATA  0, 0, 0,14, 0, 0 :REM     15
8390 DATA  0,21,17, 0, 0, 0 :REM     16
8400 DATA 12,22,18,16, 0, 0 :REM     17
8410 DATA  0,23, 0,17, 0, 0 :REM     18
8420 DATA  0,24,20, 0, 0, 0 :REM     19
8430 DATA 15,25, 0,19, 0, 0 :REM     20
8440 DATA 16,26,22, 0, 0, 0 :REM     21
8450 DATA 17, 0,23,21, 0, 0 :REM     22
8460 DATA  0, 0, 0, 0, 0, 0 :REM     23
8470 DATA 19, 0,25,23, 0, 0 :REM     24
8480 DATA 20,30, 0,24, 0, 0 :REM     25
8490 DATA 21,31, 0, 0, 0, 0 :REM     26
8500 DATA  0, 0,28, 0, 0, 0 :REM     27
8510 DATA 23,33,29, 0, 0, 0 :REM     28
8520 DATA  0,34,30,28, 0, 0 :REM     29
8530 DATA 25,35, 0,29, 0, 0 :REM     30
8540 DATA 26,36,32, 0, 0, 0 :REM     31
8550 DATA  0,37, 0,31, 0, 0 :REM     32
8560 DATA  0, 0, 0, 0, 0, 0 :REM     33
8570 DATA 29,39, 0,33, 0, 0 :REM     34
8580 DATA 30,40, 0, 0, 0, 0 :REM     35
8590 DATA 31, 0,37, 0, 0, 0 :REM     36
8600 DATA 32, 0, 0,36, 0, 0 :REM     37
8610 DATA 33, 0,39, 0, 0, 0 :REM     38
8620 DATA 34, 0,40,38, 0, 0 :REM     39
8630 DATA 35, 0, 0,39, 0, 0 :REM     40
8640 DATA 41, 0,42,41, 0, 0 :REM     41
8650 DATA 42,45,43,41, 0, 0 :REM     42
8660 DATA  0,46,43,42, 0, 0 :REM     43
8670 DATA  0, 0,45,44, 0, 0 :REM     44
8680 DATA 42,49, 0,44, 0, 0 :REM     45
8690 DATA 43, 0,47, 0, 0, 0 :REM     46
8700 DATA  0,51,47,46, 0, 0 :REM     47
8710 DATA 48,52,56,48, 0, 0 :REM     48
8720 DATA 45,53,50, 0, 0, 0 :REM     49
8730 DATA 50,54, 0,49, 0, 0 :REM     50
8740 DATA 47,55,51, 0, 0, 0 :REM     51
8750 DATA 48, 0,53, 0, 0, 0 :REM     52
8760 DATA 49, 0, 0,52, 0, 0 :REM     53
8770 DATA 50, 0,55, 0, 0, 0 :REM     54
8780 DATA 51, 0, 0,54, 0, 0 :REM     55
8790 DATA 48, 0, 0, 0, 0, 0 :REM     56
8800 DATA  0, 0, 0, 0, 0, 0 :REM     57
8810 REM *********************************
8820 REM * VOORWERPEN , PLAATS , GEWICHT *
8830 REM *********************************
8840 DATA "verband"  , 7  , 1
8850 DATA "water"    , 9  , 1
8860 DATA "zwaard"   ,99  , 3
8870 DATA "zwachtel" ,11  , 1
8880 DATA "touw"     ,12  , 1
8890 DATA "schild"   ,99  , 2
8900 DATA "ring"     ,16  , 2
8910 DATA "sleutel"  ,99  , 1
8920 DATA "drank"    ,32  , 2
8930 DATA "halssnoer",40  , 2
8940 REM **************
8950 REM * RICHTINGEN *
8960 REM **************
8970 DATA "noord"
8980 DATA "zuid"
8990 DATA "oost"
9000 DATA "west"
9010 DATA "boven"
9020 DATA "beneden"
9100 REM *************
9110 REM * LANDKAART *
9120 REM *************
9130 PRINT "Staat de printer aan en zit er papier in (j/n) ?":PRINT
9140 I$=INKEY$:IF I$="" THEN GOTO 9140
9150 IF I$="j" OR I$="J" THEN GOTO 9180
9160 IF I$="n" OR I$="N" THEN GOTO 100
9170 GOTO 9140
9180 GOSUB 20070:GOSUB 20000:                                                        X$="* HET RAADSEL VAN TRANSKAUKAZIE *":Y$="          "
9181 A=38-INT(LEN(X$)/2)
9185 LPRINT STRING$(A,32);STRING$(LEN(X$),"*")
9190 LPRINT STRING$(A,32);X$
9200 LPRINT STRING$(A,32);STRING$(LEN(X$),"*")
9211 LPRINT:LPRINT
9220 LPRINT Y$;"ฺฤฤฤฤฤฤฟ       ฺฤฤฤฤฤฟ               ฺฤฤฤฤฤฟ"
9230 LPRINT Y$;"ณ  ษอ1อฯอออป  ษฯ2ออออฯป  ษอ3อออออป  ษฯ4ออออฯป  ษอ5อออออป"
9240 LPRINT Y$;"ภฤฤถ Woud  วฤฤถ Woud  วฤฤถ Woud  วฤฤถ Woud  วฤฤถ Hut v บ"
9250 LPRINT Y$;"   บ       บ  บ       บ  บ       บ  บ       บ  บ Smid  บ"
9260 LPRINT Y$;"   ศอออัอออผ  ศอออัอออผ  ศอออออออผ  ศอออัอออผ  ศอออัอออผ"
9270 LPRINT Y$;"   ษอ6อฯอออป  ษอ7อฯอออป  ษอ8อออออป  ษอ9อฯอออป  ษ10อฯอออป"
9280 LPRINT Y$;"ฺฤฤถ Woud  บ  บ Woud  วฤฤถ Woud  วฤฤถ Woud  บ  บ Werk- บ"
9290 LPRINT Y$;"ภฤฤถ       บ  บ       บ  บ       บ  บ       บ  บ plaatsบ"
9300 LPRINT Y$;"   ศอออัอออผ  ศอออออออผ  ศอออัอออผ  ศอออออออผ  ศอออออออผ"
9310 LPRINT Y$;"   ษ11อฯอออป  ษ12อออออป  ษ13อฯอออป  ษ14อออออป  ษ15อออออป"
9320 LPRINT Y$;"ฺฤฤถ Woud  วฤฤถ Woud  วฤฤถ Woud  วฤฤถ Woud  วฤฤถ Heide บ"
9330 LPRINT Y$;"ภฤฤถ       บ  บ       บ  บ       บ  บ       บ  บ       บ"
9340 LPRINT Y$;"   ศอออออออผ  ศอออัอออผ  ศอออออออผ  ศอออออออผ  ศอออัอออผ"
9345 LPRINT
9350 LPRINT STRING$(LEN(Y$)-2,32);"- U kunt het touw hier gebruiken om in het kasteel te komen -
9360 LPRINT
9370 LPRINT Y$;"   ษ16อออออป  ษ17อฯอออป  ษ18อออออป  ษ19อออออป  ษ20อฯอออป"
9380 LPRINT Y$;"   บ Hoek  วฤฤถ Kan-  วฤฤถ Kan-  บ  บ Kan-  วฤฤถ Hoek  บ"
9390 LPRINT Y$;"   บ Toren บ  บ telen บ  บ telen บ  บ telen บ  บ Toren บ"
9400 LPRINT Y$;"   ศอออัอออผ  ศอออัอออผ  ศอออัอออผ  ศอออัอออผ  ศอออัอออผ"
9410 LPRINT Y$;"   ษ21อฯอออป  ษ22อฯอออป  ษ23อฯอออป  ษ24อฯอออป  ษ25อฯอออป"
9420 LPRINT Y$;"   บ Kan-  วฤฤถ Stal  วฤฤถ  ฤลฤ  วฤฤถ Opslagวฤฤถ Kan-  บ"
9430 LPRINT Y$;"   บ telen บ  บ       บ  บ   ณ   บ  บ plaatsบ  บ telen บ"
9440 LPRINT Y$;"   ศอออัอออผ  ศอออออออผ  ศอออัอออผ  ศอออออออผ  ศอออัอออผ"
9450 LPRINT Y$;"   ษ26อฯอออป  ษ27อออออป  ษ28อฯอออป  ษ29อออออป  ษ30อฯอออป"
9460 LPRINT Y$;"   บ Kan-  บ  บ Gevan วฤฤถ Binnenวฤฤถ Vuur  วฤฤถ Kan-  บ"
9470 LPRINT Y$;"   บ telen บ  บ genis บ  บ plaatsบ  บ kamer บ  บ telen บ"
9480 LPRINT Y$;"   ศอออัอออผ  ศอออออออผ  ศอออัอออผ  ศอออัอออผ  ศอออัอออผ"
9490 LPRINT Y$;"   ษ31อฯอออป  ษ32อออออป  ษ33อฯอออป  ษ34อฯอออป  ษ35อฯอออป"
9500 LPRINT Y$;"   บ Kan-  วฤฤถ Drank บ  บ  ฤลฤ  วฤฤถ Woon  บ  บ Kan-  บ"
9510 LPRINT Y$;"   บ telen บ  บ kamer บ  บ   ณ   บ  บ kamer บ  บ telen บ"
9520 LPRINT Y$;"   ศอออัอออผ  ศอออัอออผ  ศอออัอออผ  ศอออัอออผ  ศอออัอออผ"
9530 LPRINT Y$;"   ษ36อฯอออป  ษ37อฯอออป  ษ38อฯอออป  ษ39อฯอออป  ษ40อฯอออป"
9540 LPRINT Y$;"   บ Hoek  วฤฤถ Kan-  บ  บ Kan-  วฤฤถ Kan-  วฤฤถ Hoek  บ"
9550 LPRINT Y$;"   บ Toren บ  บ telen บ  บ telen บ  บ telen บ  บ Toren บ"
9560 LPRINT Y$;"   ศอออออออผ  ศอออออออผ  ศอออออออผ  ศอออออออผ  ศอออออออผ"
9570 LPRINT:LPRINT
9580 LPRINT Y$;"   In de onderstaande ruimtes bevinden zich bij het begin"
9590 LPRINT Y$;"   van het spel de volgende voorwerpen en/of personages :"
9600 LPRINT
9610 LPRINT Y$;"   Ruimte nr. voorwerp/personage
9620 LPRINT
9630 LPRINT Y$;"   5          Smid.Deze kan zich ook naar 10 verplaatsen."
9640 LPRINT Y$;"   7          Verband"
9650 LPRINT Y$;"   9          Beek"
9660 LPRINT Y$;"   11         2e Verband"
9670 LPRINT Y$;"   12         Touw"
9680 LPRINT Y$;"   13         Handelsreiziger en Wolf"
9690 LPRINT Y$;"   16         Ring"
9700 LPRINT Y$;"   27         Gevangene"
9710 LPRINT Y$;"   28         Monster"
9720 LPRINT Y$;"   32         Drank"
9730 LPRINT Y$;"   40         Halssnoer"
9735 FOR A=1 TO 10:LPRINT:NEXT A
9740 PRINT "Als de printer uitgeprint is moet u het papier":PRINT
9750 PRINT "omdraaien en weer terug in de printer doen .":PRINT
9760 PRINT:PRINT "Tik op een willekeurige toets om ":PRINT
9770 PRINT "verder te gaan .":PRINT
9780 I$=INKEY$:IF I$="" THEN GOTO 9780
9800 LPRINT STRING$(A,32);STRING$(LEN(X$),"*")
9810 LPRINT STRING$(A,32);X$
9820 LPRINT STRING$(A,32);STRING$(LEN(X$),"*")
9830 LPRINT:LPRINT
9840 LPRINT Y$;"ฺฤฤฤฤฤฤฟ       ฺฤฤฤฤฤฟ"
9850 LPRINT Y$;"ณ  ษ41อฯอออป  ษฯ42อออฯป  ษ43อออออป"
9860 LPRINT Y$;"ภฤฤถ Riool วฤฤถ Riool วฤฤถ Riool วฤฤฟ"
9870 LPRINT Y$;"   บ       บ  บ       บ  บ       วฤฤู"
9880 LPRINT Y$;"   ศอออออออผ  ศอออัอออผ  ศอออัอออผ"
9890 LPRINT Y$;"   ษ44อออออป  ษ45อฯอออป  ษ46อฯอออป  ษ47อออออป"
9900 LPRINT Y$;"ฺฤฤถ Riool วฤฤถ Riool บ  บ Riool วฤฤถ Riool วฤฤฟ"
9910 LPRINT Y$;"ภฤฤถ       บ  บ       บ  บ       บ  บ       วฤฤู"
9920 LPRINT Y$;"   ศอออออออผ  ศอออัอออผ  ศอออออออผ  ศอออัอออผ"
9930 LPRINT Y$;"ฺฤฤฤฤฤฤฟ          ณ       ฺฤฤฤฤฤฟ       ณ"
9940 LPRINT Y$;"ณ  ษ48อฯอออป  ษ49อฯอออป  ษฯ50อออฯป  ษ51อฯอออป"
9950 LPRINT Y$;"ภฤฤถ Riool วฤทบ Riool วฤฤถ Riool บฺฤถ Riool บ"
9960 LPRINT Y$;"   บ       บ บบ       บ  บ       บภฤถ       บ"
9970 LPRINT Y$;"   ศอออัอออผ บศอออัอออผ  ศอออัอออผ  ศอออัอออผ"
9980 LPRINT Y$;"   ษ52อฯอออป บษ53อฯอออป  ษ54อฯอออป  ษ55อฯอออป"
9990 LPRINT Y$;"   บ Riool วฤืถ Riool บ  บ Riool วฤฤถ Riool บ"
10000 LPRINT Y$;"   บ       บ บบ       บ  บ       บ  บ       บ"
10010 LPRINT Y$;"   ศอออออออผ บศอออออออผ  ศอออออออผ  ศอออออออผ"
10020 LPRINT Y$;"          ฺฤฤฝ"
10030 LPRINT Y$;"          ณ  ษ56ออป   -Deze kamer kan men zonder"
10040 LPRINT Y$;"          ภฤฤถ    บ   sleutel niet passeren !!!-"
10050 LPRINT Y$;"             ศออัอผ"
10060 LPRINT Y$;"                ณ"
10070 LPRINT Y$;"           ษ57ออฯออออป"
10080 LPRINT Y$;"           บ  Schat  บ"
10090 LPRINT Y$;"           บ  Kamer  บ"
10100 LPRINT Y$;"           ศอออออออออผ"
10110 LPRINT:LPRINT
10120 LPRINT Y$;"       Over het spel :"
10130 LPRINT
10140 LPRINT Y$;"     Het spel 'Het raadsel van Transkaukazie ' is"
10150 LPRINT Y$;"     een adventure waarbij u op de een of andere"
10160 LPRINT Y$;"     manier in 'Het Groene Woud ' terecht komt en"
10170 LPRINT Y$;"     als u dan een aantal stappen naar het Zuiden"
10180 LPRINT Y$;"     gaat , ziet u het kasteel 'De Zwarte Mongurl '"
10190 LPRINT Y$;"     liggen . Uw opdracht is om de door vele men-"
10200 LPRINT Y$;"     sen gemeden gebied te ontdoen van het monster"
10210 LPRINT Y$;"     dat al eeuwen door het kasteel rondzwerft ."
10220 LPRINT Y$;"     Het is een gevaarlijk spel waarbij u dingen"
10230 LPRINT Y$;"     moet oppakkken om iemand te helpen . Verder"
10240 LPRINT Y$;"     zal ik niet te veel verklappen ."
10250 LPRINT
10260 LPRINT Y$;"     Veel plezier in 'HET RAADSEL VAN TRANSKAUKAZIE '"
10270 FOR I=1 TO 10:LPRINT :NEXT I:PRINT "Druk op een toets om te beginnen":          WHILE INKEY$="":WEND:GOSUB 20070:RETURN
20000 REM NLQ
20010 OPEN "LPT1:" AS #1
20020 WIDTH #1,255
20030 PRINT #1,CHR$(10);
20040 PRINT #1,CHR$(27);"x";CHR$(1);
20050 CLOSE
20060 RETURN
20070 REM PRINTER RESET (ESC,@)
20080 OPEN "LPT1:" AS #1
20090 WIDTH #1,255
20100 REM RESET PRINTER
20110 PRINT #1,CHR$(27);"@";
20120 PRINT #1,CHR$(10);
20130 CLOSE
20140 RETURN
