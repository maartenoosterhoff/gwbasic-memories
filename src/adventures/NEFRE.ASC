10 REM --- DE VLOEK VAN NEFRE A TOTH ---
15 CLS
20 GOSUB 6000:REM INITIATIE
100 REM ************
110 REM * HOOFDLUS *
120 REM ************
130 GOSUB 1000:REM COMMANDO
140 REM PLAATSVERANDERING
150 IF O<C1 AND C1<7 THEN GOSUB 1300:GOTO 190
160 REM NEEM, LEG, INVENTARIS
170 ON C1-6 GOSUB 1400, 1500, 1600
180 REM KAMERBESCHRIJVING
190 PRINT
200 IF KA<=7 THEN ON KA GOSUB 4030,4080,4130,4170,4220,4270,4330:GOTO 240
210 IF KA<=14 THEN ON KA- 7 GOSUB 4370,4430,4460,4510,4540,4590,4650: GOTO 240
220 IF KA<=21 THEN ON KA-14 GOSUB 4690,4740,4800,4850,4910: GOTO 240
230 REM VOORWERPEN
240 IF FLOW=1 THEN GOSUB 2000
250 REM DOORSPELEN, GEWONNEN, VERLOREN
260 IF FLOW=1 THEN GOTO 100
270 IF FLOW=3 THEN GOTO 400
300 REM ************
310 REM * GEWONNEN *
320 REM ************
330 PRINT: PRINT "Via een bochterige grot bereikt u weer":PRINT
340 PRINT "De bewoonde wereld . Rijk en gezond keert u weer huiswaarts ."
345 PRINT INPUT$(1)
350 END
400 REM ************
410 REM * VERLOREN *
420 REM ************
430 PRINT
440 PRINT "Plotseling klinkt er een":PRINT
450 PRINT "verschrikkelijke kreet .":PRINT
460 PRINT "Het loopt niet goed met u af .";INPUT$(1)
470 END
1000 REM *********************
1010 REM * COMMANDO VERTALER *
1020 REM *********************
1030 PRINT: INPUT "Wat doet u";C$
1040 IF C$="" THEN GOTO 1030
1045 PRINT
1050 PO=LEN(C$)+1
1060 FOR I=1 TO LEN(C$)
1070 IF ASC(MID$(C$,I))=32 THEN PO=I
1080 NEXT I
1090 C1=0: LET W$=MID$ (C$,1,PO-1)
1100 IF W$="noord"      THEN C1=1
1110 IF W$="zuid"       THEN C1=2
1120 IF W$="oost"       THEN C1=3
1130 IF W$="west"       THEN C1=4
1140 IF W$="op"         THEN C1=5
1150 IF W$="neer"       THEN C1=6
1160 IF W$="neem"       THEN C1=7
1170 IF W$="leg"        THEN C1=8
1180 IF W$="inventaris" THEN C1=9
1190 IF C1=0 THEN GOTO 1030
1200 IF PO=LEN(C$)+1 THEN RETURN
1210 C2=0: LET W$=MID$(C$,PO+1,LEN(C$))
1220 IF W$="amulet"     THEN C2=1
1230 IF W$="zwaard"     THEN C2=2
1240 IF W$="schaal"     THEN C2=3
1250 IF W$="beeld"      THEN C2=4
1260 IF W$="spiegel"    THEN C2=5
1270 IF C2=0 THEN GOTO 1030
1280 RETURN
1300 REM **********************
1310 REM * PLAATS VERANDERING *
1320 REM **********************
1330 IF K%(KA,C1)=0 THEN PRINT "Dat kan niet .":PRINT:RETURN
1340 KA=K%(KA,C1)
1350 RETURN
1400 REM ********
1410 REM * NEEM *
1420 REM ********
1430 IF OB% (C2)<>KA THEN PRINT "Dat ligt hier niet .":PRINT:RETURN
1440 OB% (C2)=0
1450 PRINT "U neemt het voorwerp .":PRINT
1460 RETURN
1500 REM *******
1510 REM * LEG *
1520 REM *******
1530 IF OB%(C2)<>0 THEN PRINT "Dat heeft u niet .":PRINT:RETURN
1540 OB%(C2)=KA
1550 PRINT "U legt het neer .":PRINT
1560 RETURN
1600 REM **************
1610 REM * INVENTARIS *
1620 REM **************
1630 HE=0
1640 PRINT "U heeft";
1650 FOR I=1 TO NO
1660 IF OB%(I)=0 THEN PRINT " een ";T$(I);" en";: HE=1
1670 NEXT I
1680 IF HE=0 THEN PRINT " geen voorwerp ."
1685 PRINT
1690 RETURN
2000 REM **************
2010 REM * VOORWERPEN *
2020 REM **************
2030 HE=0
2040 PRINT:PRINT "Hier ligt";
2050 FOR I=1 TO NO
2060 IF OB%(I)=KA THEN PRINT " een ";T$(I);" en";: HE=1
2065 IF I=NO THEN PRINT
2070 NEXT I
2080 IF HE=0 THEN PRINT:PRINT " geen voorwerp .":PRINT
2090 RETURN
3000 REM ***********
3010 REM * MONSTER *
3020 REM ***********
3030 PRINT:PRINT "Er is hier een verschrikkelijk monster .":PRINT
3040 PRINT "Het beest briest vuur...":PRINT
3050 INPUT "Vlucht... of vecht .";C$:PRINT
3060 IF C$="vecht" THEN GOTO 3090 ELSE IF C$="vlucht" THEN GOTO 3070 ELSE GOTO 3050
3070 PRINT "U kunt naar alle richtingen vluchten .":PRINT
3080 RETURN
3090 REM * VECHT *
3100 IF OB% (1)=0 AND OB%(2)=0 THEN GOTO 3200
3110 PRINT:PRINT "Helaas heeft u geen ";
3120 IF OB%(1)<>0 THEN PRINT "kracht ";
3130 IF OB%(1)<>0 AND OB%(2)<>0 THEN PRINT "en geen ";
3140 IF OB%(2)<>0 THEN PRINT "zwaard";
3150 PRINT "..."
3160 PRINT "Het verschrikkelijke monster valt aan .":PRINT
3170 PRINT "Ondanks uw moedige pogingen u te":PRINT
3180 PRINT "verdedigen raakt u dodelijk verwondt .":PRINT
3190 LET FLOW=3:RETURN
3200 PRINT "Gelukkig beschikt u over een zwaard en ":PRINT
3210 PRINT "een amulet . U haalt uit...":PRINT
3220 PRINT "en treft het monster in de hartstreek .":PRINT
3230 PRINT "Het beest valt verslagen neer .":PRINT
3240 MS=1:REM MONSTER IS DOOD
3250 PRINT "U kunt uw tocht weer voortzetten .":PRINT
3260 RETURN
4000 REM **********************
4010 REM * KAMER BESCHRIJVING *
4020 REM **********************
4030 REM KAMER 1
4040 PRINT "U bevindt zich in een vierkante ruimte .":PRINT
4050 PRINT "In het zuiden is een uitgang .":PRINT
4060 PRINT "Op de wanden staat : Wee u voor de vloek van Nefre A Toth !":PRINT
4070 RETURN
4080 REM KAMER 2
4090 PRINT "U bevindt zich in een kamer met in elke muur een uitgang .":PRINT
4100 PRINT "Op de wand staat : Mijn leven is voor":PRINT
4110 PRINT "Hokap , hij is almachtig .":PRINT
4120 RETURN
4130 REM KAMER 3
4140 PRINT "U bevindt zich in een vreemde donkere":PRINT
4150 PRINT "ruimte .":PRINT
4160 RETURN
4170 REM KAMER 4
4180 PRINT "U bevindt zich in een grote ruimte .":PRINT
4190 PRINT "Op de wand staan dreigende afbeeldingen van Nefre A Toth .":PRINT
4200 FLOW=3: REM ONGELUKKIG EINDE
4210 RETURN
4220 REM KAMER 5
4230 PRINT "U bevindt zich in een vierkante ruimte":PRINT
4240 PRINT "met veel uitgangen . Op de wand staat :":PRINT
4250 PRINT "'Het spiegelbeeld van nevel is leven .'":PRINT
4260 RETURN
4270 REM KAMER 6 (MONSTER)
4280 PRINT "U bevindt zich in een enorme ruimte .":PRINT
4290 PRINT "In alle richtingen zijn uitgangen .":PRINT
4300 IF MS=1 THEN PRINT "In de hoek ligt een met bloed besmeurd beest .":PRINT
4310 IF MS=0 THEN GOSUB 3000
4320 RETURN
4330 REM KAMER 7
4340 PRINT "U bent in een klein kamertje .":PRINT
4350 IF OB%(3)=17 THEN LET K%(7,6)=8:PRINT "Op de bodem is een open luik .":PRINT
4360 RETURN
4370 REM KAMER 8
4380 PRINT "U bevindt zich in een kleine hal waar":PRINT
4390 PRINT "veel gangen op uitkomen . Er loopt een":PRINT
4400 PRINT "trap naar boven . Op de wand staat :":PRINT
4410 PRINT "'Keer terug... Het is nog niet te laat !":PRINT
4420 RETURN
4430 REM KAMER 9
4440 PRINT "U bevindt zich in in een lange gang .":PRINT
4450 RETURN
4460 REM KAMER 10
4470 PRINT "U bevindt zich in een prachtige":PRINT
4480 PRINT "beschilderde hal . Dit is de hal van":PRINT
4490 PRINT "Nefre A Toth en zij bewaart haar geheim .":PRINT
4500 RETURN
4510 REM KAMER 11
4520 PRINT "U bevindt zich in een lange gang":PRINT
4530 RETURN
4540 REM KAMER 12
4550 PRINT "U bevindt zich in een prachtige hal .":PRINT
4560 PRINT "Op de wand staat : 'De nevel van mijn":PRINT
4570 PRINT "leven... Daar gaat het om .":PRINT
4580 RETURN
4590 REM KAMER 13
4600 PRINT "U bevindt zich in een korte gang met":PRINT
4610 PRINT "veel inscripties .":PRINT
4620 IF OB%(4)=10 AND OB% (5)=12 THEN GOTO 4640
4630 PRINT "De zuid-deur is dicht .":PRINT:RETURN
4640 K%(13,2)=18: PRINT "De zuid-deur gaat open .":PRINT:RETURN
4650 REM KAMER 14
4660 PRINT "U bevindt zich in een prachtige ronde":PRINT
4670 PRINT "hal . De wanden staren u dreigend aan .":PRINT
4680 RETURN
4690 REM KAMER 15
4700 PRINT "U bevindt zich in een merkwaardige":PRINT
4710 PRINT "ruimte . Er klopt hier iets niet...":PRINT
4720 FLOW=3:REM EIND
4730 RETURN
4740 REM KAMER 16
4750 PRINT "U bevindt zich in een lange rechte gang .":PRINT
4760 PRINT "Er zijn uitgangen in het oosten en het":PRINT
4770 PRINT "zuiden .":PRINT
4780 PRINT "Op de wand staat : 'Ga niet verder...":PRINT
4790 RETURN
4800 REM KAMER 17
4810 PRINT "U bevindt zich in een merkwaardige":PRINT
4820 PRINT "ruimte . Op de wand staat :":PRINT
4830 PRINT "'Breng mijn geluk terug .'":PRINT
4840 RETURN
4850 REM KAMER 18
4860 PRINT "Voor u ligt de prachtige schatkamer .":PRINT
4870 PRINT "U bent de eerste die deze kamer heeft":PRINT
4880 PRINT "ontdekt .":PRINT
4890 FLOW=2: REM GELUKKIG EIND
4900 RETURN
4910 REM KAMER 19
4920 PRINT "U bevindt zich in een klein halletje .":PRINT
4930 PRINT "Er zij uitgangen in noordelijke en":PRINT
4940 PRINT "oostelijke richting .":PRINT
4950 IF OB%(1)=0 THEN FLOW=3: REM AMULET BRENGT ONGELUK
4960 RETURN
6000 REM *************
6010 REM * INITIATIE *
6020 REM *************
6030 NK=19:NO=5
6040 MS=0: REM MONSTER LEEFT
6050 FLOW=1:REM SPELER LEEFT
6060 KA=1:REM START-POSITIE
6070 DIM K%(NK,6), OB%(NO), T$(NO)
6080 FOR K=1 TO NK
6090 FOR J=1 TO 6
6100 READ K% (K,J): REM KAMER VERBINDINGEN
6110 NEXT J
6120 NEXT K
6130 FOR K=1 TO NO
6140 READ T$(K),OB%(K): REM VOORWERP, PLAATS
6150 NEXT K
6160 PRINT:PRINT
6170 PRINT "U bent zojuist via de westelijke ingang":PRINT
6180 PRINT "de graftombe van Nefre A Toth binnen":PRINT
6190 PRINT "gekomen . Helaas de ingang stort dicht...":PRINT
6200 PRINT "Gelukkig schijnt uw lantaarn nog...":PRINT
6210 RETURN
6300 REM **********************
6310 REM * KAMER VERBINDINGEN *
6320 REM **********************
6330 DATA  0, 2, 0, 0, 0, 0
6340 DATA  1, 4, 3,16, 0, 0
6350 DATA  0, 5,15, 2, 0, 0
6360 DATA  2, 0, 5, 0, 0, 0
6370 DATA  3,17, 6, 4, 0, 0
6380 DATA 15, 7,14, 5, 0, 0
6390 DATA  6, 0, 0, 0, 0, 0
6400 DATA  0,13,11, 9, 7, 0
6410 DATA  0, 0, 8,10, 0, 0
6420 DATA  0, 0, 9, 0, 0, 0
6430 DATA  0, 0,12, 8, 0, 0
6440 DATA  0, 0, 0,11, 0, 0
6450 DATA  8, 0, 0, 0, 0, 0
6460 DATA  0, 0, 0, 6, 0, 0
6470 DATA  0, 6, 0, 3, 0, 0
6480 DATA  0,19, 2, 0, 0, 0
6490 DATA  5, 0, 0,19, 0, 0
6500 DATA 13, 0, 0, 0, 0, 0
6510 DATA 16, 0,17, 0, 0, 0
6600 REM **********************
6610 REM * VOORWERPEN, PLAATS *
6620 REM **********************
6630 DATA  "amulet", 3
6640 DATA  "zwaard",14
6650 DATA  "schaal", 2
6660 DATA   "beeld",12
6670 DATA "spiegel",10
